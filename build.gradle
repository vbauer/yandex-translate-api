
plugins {
    id 'java'
    id 'groovy'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'ru.vyarus.quality' version '2.4.0'
    id 'ru.vyarus.java-lib' version '1.1.2'
    id 'net.ltgt.apt-idea' version '0.18'
}

group 'com.github.vbauer'
version '1.4.1'
description 'A simple REST client library for Yandex.Translate'

repositories {
    mavenLocal()
    mavenCentral()
}

configurations.provided.extendsFrom configurations.annotationProcessor

ext.deps = [
    gson: '2.8.5',
    fbAnnotations: '3.0.1',
    immutables: '2.7.1',

    spock: '1.1-groovy-2.4',
    jpcc: '1.2.0'
]

dependencies {
    compile "com.google.code.gson:gson:${deps.gson}"
    compile "com.google.code.findbugs:annotations:${deps.fbAnnotations}"

    annotationProcessor "org.immutables:value:${deps.immutables}"
    annotationProcessor "org.immutables:builder:${deps.immutables}"
    annotationProcessor "org.immutables:gson:${deps.immutables}"

    testCompile "org.spockframework:spock-core:${deps.spock}"
    testCompile "com.pushtorefresh.java-private-constructor-checker:checker:${deps.jpcc}"
}

wrapper {
    gradleVersion = '4.10.2'
    distributionType = Wrapper.DistributionType.ALL
}

compileJava {
    String javaVersion = JavaVersion.VERSION_1_8

    sourceCompatibility javaVersion
    targetCompatibility javaVersion
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

pom {
    inceptionYear '2015'
    licenses {
        license {
            name 'MIT License'
            url 'http://www.opensource.org/licenses/mit-license.php'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id 'vbauer'
            name 'Vladislav Bauer'
            email 'bauer.vlad@gmail.com'
            url 'http://linkedin.com/in/vladislavbauer'
            roles {
                role 'Developer'
            }
        }
    }
    issueManagement {
        system 'GitHub Issues'
        url 'https://github.com/vbauer/yandex-translate-api/issues'
    }
    ciManagement {
        system 'Travis'
        url 'https://travis-ci.org/vbauer/yandex-translate-api'
    }
}
